//==============================================================================
// cloudDome.kbShader
//
// 2019	 kbEngine 2.0
//==============================================================================
cbuffer matrixBuffer {
	matrix mvpMatrix;
	matrix modelMatrix;
	matrix viewProjection;
	float4 cameraPos;
	float4 time;
	float4 cloudTileAndOffset;
	float4 cloud12_ScrollRate;
	float4 cloud34_ScrollRate;
	float4 cloudColor;
};

//-------------------------------------
struct vertexInput {
	float4 position   : POSITION;
	float2 uv         : TEXCOORD0;
	float4 color      : COLOR;
	float4 normal     : NORMAL;
	float4 tangent    : TANGENT;
};

//-------------------------------------
struct pixelInput {
	float4 position		: SV_POSITION;
	float2 uv			: TEXCOORD0;
	float4 color		: COLOR;
	float4 position2	: TEXCOORD1;
	float3 normal		: TEXCOORD2;
	float3 worldPos		: TEXCOORD3;
};

kbShaderState {
	cullMode CullMode_None
	srcblend Blend_One
	dstBlend Blend_One
	colorwriteenable colorwriteenable_rgb
}

//-------------------------------------
struct PS_OUTPUT {
	float4 Color		: SV_TARGET0;
	float2 Depth		: SV_TARGET1;
};

/**
 *	vertexShader
 */
pixelInput vertexShader( vertexInput input ) {
	pixelInput output = (pixelInput)(0);

	output.position = mul( input.position, modelMatrix );
	output.worldPos.xyz = cameraPos.xyz + output.position.xyz * 999;
	output.position += float4(cameraPos.xyz, 0);
	output.position = mul( output.position, viewProjection);
	output.position.z = output.position.w - 0.0001f;

	output.uv = input.uv;
	output.color = input.color;
	output.position2 = output.position;
	output.normal.xyz = mul( ( input.normal.xyz * 2.0f ) - 1.0f, (float3x3)modelMatrix );

	return output;
}


Texture2D diffuseTexture;
SamplerState LinearSampler;

/**
 *	pixelShader
 */
 PS_OUTPUT pixelShader( pixelInput	input ) {
 	PS_OUTPUT output = (PS_OUTPUT) 0;

	float2 uv = input.uv * cloudTileAndOffset.xy;


	const float cloudNoisePower = 3.5f;
	const float combinedCloudFactor = 2.0f;
	const float combinedCloudPower = 2.7f;
	const float cloudDivisor = 3;
	const float gimmeSomeMo = 1.35f;																																
	float4 cloudLayer1 = pow( abs( diffuseTexture.Sample( LinearSampler, uv + time.xx * cloud12_ScrollRate.xy * gimmeSomeMo ).r ), abs( cloudNoisePower ) );
	float4 cloudLayer2 = pow( abs( diffuseTexture.Sample( LinearSampler, uv + time.xx * cloud12_ScrollRate.zw * gimmeSomeMo ).g ), abs( cloudNoisePower ) );
	float4 cloudLayer3 = pow( abs( diffuseTexture.Sample( LinearSampler, uv + time.xx * cloud34_ScrollRate.xy * gimmeSomeMo ).b ), abs( cloudNoisePower ) );
	float4 cloudLayer4 = pow( abs( diffuseTexture.Sample( LinearSampler, uv + time.xx * cloud34_ScrollRate.zw * gimmeSomeMo ).r ), abs( cloudNoisePower ));

	float4 totalCloud = cloudLayer1 + cloudLayer2 + cloudLayer3 + cloudLayer4;
	totalCloud /= cloudDivisor;
	output.Color.xyz = saturate( pow( abs(totalCloud.xyz), abs( combinedCloudPower ) ) ) * combinedCloudFactor;
	output.Color.w = dot( totalCloud.xyz, float3( 0.3, 0.59f, 0.11f ) );


	float edgeXAlpha = min( smoothstep( 0.0f, 0.25f, input.uv.x ), 1.0f - smoothstep( 0.75f, 1.0f, input.uv.x ) );
	float edgeYAlpha = min( smoothstep( 0.0f, 0.25f, input.uv.y ), 1.0f - smoothstep( 0.75f, 1.0f, input.uv.y ) );
	float edgeAlpha = min( edgeXAlpha, edgeYAlpha );
	output.Color.w *= edgeAlpha;
	output.Color.xyz *= output.Color.w * cloudColor.xyz;

	if ( output.Color.w < 0.4f ) {
		output.Depth.r = 0;
		output.Depth.g = 0.0f;
	} else {
		output.Depth.r = input.position2.z / input.position2.w;// / input.position2.y;
		output.Depth.g = 1;
	}

//	output.Color.w = 0;//max( max( output.Color.x, output.Color.y ), output.Color.z ) * 1.0;
	return output;
}
